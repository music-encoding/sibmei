{
  SibmeiExtensionAPIVersion "2.0.0"

  Initialize "() {
    AddToPluginsMenu('Sibmei extension test', 'Run');
  }"

  Run "() {
    // The plugin will be listed in the menu, but it is not runnable. Give some
    // instructions instead of showing no response when users try to run it.
    Sibelius.MessageBox(
      'This plug-in is an extension of the sibmei MEI export plug-in. To use it, run MEI export.'
    );
  }"

  InitSibmeiExtension "(api) {
    Self._property:api = api;
    Self._property:libmei = api.libmei;

    Self._property:MySymbolTemplate = CreateSparseArray('Symbol', CreateDictionary(
      'fontfam', 'myCustomFont',
      'glyph.name', 'mySymbolGlyph'
    ));

    api.RegisterSymbolHandlers(CreateDictionary(
      'Name', CreateDictionary(
        'My symbol', 'HandleMySymbol'
      )
    ), Self);

    api.RegisterTextHandlers(CreateDictionary(
      'StyleAsText', CreateDictionary(
        // Example of a text generated by means of a custom text handler
        'XPath test', 'HandleXPathTest',
        // Example of a text generated by template
        'My text', CreateSparseArray('AnchoredText', CreateDictionary(), api.AddFormattedText)
      )
    ), Self);

    api.RegisterLineHandlers(CreateDictionary(
      'StyleAsText', CreateDictionary(
        'My line', CreateSparseArray(
          'Line', CreateDictionary('type', 'myline', 'endid', 'PreciseMatch')
        )
      )
    ), Self);
  }"

  HandleMySymbol "(this, obj) {
    symbolElement = api.GenerateControlEvent(obj, MySymbolTemplate);
    if (obj.ColorRed = 255) {
      libmei.AddAttribute(symbolElement, 'type', 'myRedType');
    }
    return symbolElement;
  }"

  HandleXPathTest "(this, textObj) {
    // This text style is used by the sibmei test suite
    annotElement = api.GenerateControlEvent(textObj, 'Annot');
    libmei.AddAttribute(annotElement, 'type', 'xpath-test');
    textWithFormatting = textObj.TextWithFormatting;
    for i = 0 to textWithFormatting.NumChildren {
      if (CharAt(textWithFormatting[i], 0) = '\\') {
        // Remove formatting and newlines. fontoxpath will complain if there is
        // additional whitespace in XPath expressions.
        textWithFormatting[i] = '';
      }
    }
    libmei.SetText(annotElement, JoinStrings(textWithFormatting, ''));
    return annotElement;
  }"
}
