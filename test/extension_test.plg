{
  SibmeiExtensionAPIVersion "2.0.0"

  Initialize "() {
    AddToPluginsMenu('Sibmei extension test', 'Run');
  }"

  Run "() {
    // The plugin will be listed in the menu, but it is not runnable. Give some
    // instructions instead of showing no response when users try to run it.
    Sibelius.MessageBox(
      'This plug-in is an extension of the sibmei MEI export plug-in. To use it, run MEI export.'
    );
  }"

  InitSibmeiExtension "(api) {
    Self._property:MySymbolTemplate = @Element('dir', null, @Element('symbol', @Attrs(
      'fontfam', 'myCustomFont',
      'glyph.name', 'mySymbolGlyph'
    )));

    api.RegisterSymbolHandlers('Name', 'HandleMySymbol', CreateDictionary(
      'My symbol', null
    ));

    api.RegisterSymbolHandlers('Name', 'ModifierTemplateHandler', CreateDictionary(
      'My soft accent', @Element('artic', @Attrs(
        'artic', 'acc',
        'glyph.auth', 'smufl',
        'glyph.name', 'articSoftAccentAbove',
        'glyph.num', 'U+ED40'
      ))
    ));

    api.RegisterTextHandlers('StyleAsText', 'HandleXPathTest', CreateDictionary(
      // Example of a text generated by means of a custom text handler
      'XPath test', null
    ));

    api.RegisterTextHandlers('StyleAsText', 'ControlEventTemplateHandler', CreateDictionary(
      // Example of a text generated by template
      'My text', @Element('dir', null, @Element('anchoredText', @Attrs(), api.FormattedText))
    ));

    api.RegisterTextHandlers('StyleId', 'ControlEventTemplateHandler', CreateDictionary(
      // Example of a overridden built-in style
      'text.staff.plain', @Element('dir', @Attrs(), api.UnformattedText)
    ));

    api.RegisterLineHandlers('StyleAsText', 'ControlEventTemplateHandler', CreateDictionary(
      'My line', @Element('line', @Attrs(
        'type', 'myline', 'endid', 'PreciseMatch'
      ))
    ));

    api.RegisterLyricHandlers('StyleAsText', 'LyricTemplateHandler', CreateDictionary(
      'My italic lyrics', @Element('verse', null,
        @Element('syl', null,
          @Element('rend', @Attrs('rend', 'italic'), api.LyricText)
        )
      )
    ));
  }"

  HandleMySymbol "(api, obj) {
    symbolElement = api.GenerateControlEvent(obj, api.MeiFactory(MySymbolTemplate, obj));
    if (obj.ColorRed = 255) {
      api.libmei.AddAttribute(symbolElement, 'type', 'myRedType');
    }
    return symbolElement;
  }"

  HandleXPathTest "(api, textObj) {
    // This text style is used by the sibmei test suite
    annotElement = api.AppendToMeasure(api.libmei.CreateElement('annot'));
    api.libmei.AddAttribute(annotElement, 'type', 'xpath-test');
    textWithFormatting = textObj.TextWithFormatting;
    for i = 0 to textWithFormatting.NumChildren {
      if (CharAt(textWithFormatting[i], 0) = '\\') {
        // Remove formatting and newlines. fontoxpath will complain if there is
        // additional whitespace in XPath expressions.
        textWithFormatting[i] = '';
      }
    }
    api.libmei.SetText(annotElement, JoinStrings(textWithFormatting, ''));
    return annotElement;
  }"
}
